<?xml version="1.0" encoding="UTF-8"?>

<xsl:stylesheet 
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:bml="http://efele.net/2010/ns/bml"
  xmlns:dc="http://purl.org/dc/elements/1.1/"
  xmlns:opf="http://www.idpf.org/2007/opf"
  exclude-result-prefixes="bml"
  version="2.0">


<xsl:template match='bml:metadata' mode='dc'>

  <dc:title id="dc_title1"><xsl:value-of select="bml:electronique/bml:titre"/></dc:title>

<!--
  <meta refines="#dc_title1" property='title-type'>main</meta>
  
  <xsl:if test='bml:electronique/bml:collection'>
    <dc:title id="dc_title2"><xsl:value-of select="bml:electronique/bml:collection/bml:titre"/></dc:title>
    <meta refines="#dc_title2" property='title-type'>collection</meta>
    <xsl:if test='bml:electronique/bml:collection/@volume'>
      <meta refines="#dc_title2" property='group-position'>
        <xsl:value-of select='bml:electronique/bml:collection/@volume'/>
      </meta>
    </xsl:if>
  </xsl:if>
-->

  <xsl:for-each select='bml:monographie/bml:auteur|bml:article/bml:auteur'>
    <xsl:element name='{if (@role = "aut") then "dc:creator"
                        else if (@role) then "dc:contributor"
                        else "dc:creator"}'>
      <xsl:attribute name='opf:role'>
        <xsl:value-of select='if (@role) then @role else "aut"'/>
      </xsl:attribute>
      <xsl:attribute name='opf:file-as'>
        <xsl:value-of select='bml:nom-bibliographie'/>
      </xsl:attribute>
      <xsl:value-of select='bml:nom-couverture'/>
    </xsl:element>
  </xsl:for-each>
  
  <xsl:for-each select='bml:monographie/bml:editeur'>
    <dc:publisher><xsl:value-of select="bml:nom"/>, <xsl:value-of select="bml:ville"/></dc:publisher>
  </xsl:for-each>
  
  <dc:language><xsl:value-of select="bml:*/bml:langue"/></dc:language>
  
  <dc:date>
    <xsl:apply-templates select='bml:monographie/bml:date' mode='dc'/>
    <xsl:apply-templates select='bml:article/bml:numero/bml:date' mode='dc'/>
  </dc:date>
  
  <xsl:for-each select='bml:electronique/bml:producteur'>
    <dc:contributor opf:role='bkd'><xsl:value-of select="bml:nom"/></dc:contributor>
  </xsl:for-each>
  
  <dc:identifier id="uniqueId">
    <xsl:value-of select="bml:electronique/@identificateur"/>
  </dc:identifier>
  
  <xsl:for-each select='bml:volume/bml:facsimile'>
    <dc:source><xsl:value-of select='@href'/></dc:source>
  </xsl:for-each>
  
  <dc:relation id='catalogue-bnf'>
    <xsl:value-of select='bml:monographie/@bnf'/>
    <xsl:value-of select='bml:article/bml:periodique/@bnf'/>
  </dc:relation>
</xsl:template>

<xsl:template match='bml:date[@iso]' mode='dc'>
  <xsl:value-of select='@iso'/>
</xsl:template>

<xsl:template match='bml:date' mode='dc'>
  <xsl:value-of select="."/>
</xsl:template>

</xsl:stylesheet>

